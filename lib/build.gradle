plugins {
    id 'org.jetbrains.kotlin.multiplatform'
    id 'maven-publish'
}
group = 'com.github.YvesCheung'

kotlin {
    js {
        nodejs()
    }
    jvm()
    sourceSets {
        commonMain {
            dependencies {
                implementation(kotlin("stdlib-common"))
            }
        }
        commonTest {
            dependencies {
                implementation(kotlin("test-common"))
                implementation(kotlin("test-annotations-common"))
            }
        }
        jvmMain {
            dependencies {
                implementation(kotlin("stdlib-jdk8"))
            }
        }
        jsMain {
            dependencies {
                implementation(kotlin("stdlib-js"))
            }
        }
        jvmTest {
            dependencies {
                implementation(kotlin("test"))
                implementation(kotlin("test-junit"))
            }
        }
        jsTest {
            dependencies {
                implementation(kotlin("test-js"))
            }
        }
    }
}

task cleanPublish(type: Delete) {
    group = "nodejs"
    delete file("${project.projectDir}/js/kotlin")
}

task prepareForPublish(type: Copy) {
    group = "nodejs"
    dependsOn cleanPublish
    dependsOn assemble
    from file("$rootDir/build/js/packages/EmojiReader-lib/kotlin")
    into file("${project.projectDir}/js/kotlin")
}

task publishNpm {
    group = "nodejs"
    dependsOn prepareForPublish
    doLast {
        String path = "${project.projectDir}/js"
        project.exec {
            println("npm publish $path")
            commandLine("npm", "publish", path)
        }
    }
}

task mavenSourcesJar(type: Jar) {
    classifier = 'sources'
    from kotlin.sourceSets.jvmMain.kotlin.srcDirs
}

artifacts {
    archives mavenSourcesJar
}